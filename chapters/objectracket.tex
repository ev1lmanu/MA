\section{Das Objektsystem von Racket}
Als Grundlage für dieses Kapitel dient die Racketdokumentation \cite{racketguide-classes} und -Referenz \cite{racketref-classes} für Klassen und Objekte.

Racket ist eine Multipurpose-Sprache und erlaubt die Auswahl der Syntax auf Sprachebene. Eine einzelne Codezeile bestimmt die Sprache eines Moduls, also beispielsweise, ob Funktionen vorgezogen ausgewertet werden (\texttt{\#lang racket}) oder verzögert (\texttt{\#lang lazy}). Da das Objektsystem in die Sprache Racket integriert ist, ist es direkt in jedem Racketmodul verwendbar.

Die wichtigsten Werkzeuge, die ein Programmierer in einer objektorientierten Sprache benötigt, sind das Definieren von Klassen, Feldern und Funktionen sowie das Erzeugen von Objekten. Auf diese soll deshalb im Folgenden kurz eingegangen werden, bevor die bereits vorhandenen Möglichkeiten in Bezug auf Mehrfachvererbung im Objektsystem von Racket betrachtet werden.

Alle Codebeispiele befinden sich auch auf der beiliegenden CD und sind noch einmal zusammenhängend in Anhang \ref{or-example} aufgeführt. 

\subsection{Einfache Klassen}

Eine Klasse wird in Racket durch das Schlüsselwort \texttt{class} definiert. Bei der Definition einer Klasse muss die Superklasse angegeben werden. Falls die Klasse keine (andere) Superklasse hat, wird \texttt{object\%} angegeben, die eingebaute Wurzelklasse. Per Konvention sollen Klassennamen in Racket auf \% enden, bei den folgenden Beispielen wird jedoch darauf verzichtet, um sie später leichter mit CLOS vergleichen zu können. Nach der Superklasse können noch beliebige Klassenoptionen, Felder oder Methoden definiert werden. An einer beliebigen Stelle im Rumpf der Klasse muss jedoch mit \texttt{super-new} der Konstruktor der Oberklasse aufgerufen werden. Eine minimale Klassendefinition sieht damit folgendermaßen aus:

\begin{lstlisting}
(class object% (super-new))
\end{lstlisting}

Der Rückgabewert ist ein Klassenobjekt und dieses kann für den späteren Zugriff einer Variablen zugewiesen werden. In diesem Beispiel ist der Name der Klasse ``Thing''.

\begin{lstlisting}
(define Thing (class object% (super-new)))
\end{lstlisting}

Objekte von Klassen lassen sich mit den Schlüsselwörtern \texttt{new}, \texttt{make-object} und \texttt{instantiate} erzeugen, je nachdem ob Initialisierungsargumente durch Name, Position oder auf beide Arten angegeben werden sollen. Im Folgenden findet die Objekterzeugung hauptsächlich mit \texttt{new} statt und somit die Initialisierung
von Feldern durch Angabe des Namens:

\begin{lstlisting}
(new Thing)
\end{lstlisting}

Felder lassen sich mit \texttt{init-field} oder \texttt{field} deklarieren, je nachdem, ob es möglich sein soll, sie bei der Objekterzeugung zu initialisieren oder nicht. Die Klasse Thing kann beispielsweise ein Feld besitzen, das eine Beschreibung des Objekts enthält:

\begin{lstlisting}
(define Thing (class object% (super-new)
                (init-field [name "a Thing"])))
\end{lstlisting}

Auf die Felder kann mit \texttt{get-field} beziehungsweise \texttt{set-field!} zugegriffen werden. 

\begin{lstlisting}
> (get-field name (new Thing))
\end{lstlisting}
{\routput {\qq}a Thing{\qq}}

\begin{lstlisting}
(define bob (new Thing [name "Bob"]))

> (get-field name bob)
\end{lstlisting}
{\routput {\qq}Bob{\qq}}

\begin{lstlisting}
> (set-field! name bob "not Bob")
> (get-field name bob)
\end{lstlisting}
{\routput {\qq}not Bob{\qq}}

Für Methoden gibt es, je nach Art und Sichtbarkeit, unter anderem die Schlüsselwörter \texttt{define/public}, \texttt{define/private} und \texttt{define/override}. Thing kann beispielsweise eine Methode \texttt{who-are-you?} anbieten, die einen beschreibenden Text für das Objekt ausgibt:

\begin{lstlisting}
(define Thing (class object% (super-new)
                (init-field [name "a Thing"])
                (define/public (who-are-you?) 
                  (string-append "I am " name "!"))))
\end{lstlisting}

Die Methode lässt sich anschließend mittels \texttt{send} aufrufen.

\begin{lstlisting}
(send (new Thing) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am a Thing!{\qq}}

\begin{lstlisting}
(send bob who-are-you?)
\end{lstlisting}
{\routput {\qq}I am not Bob!{\qq}}

Attribute, die keinen Standardwert haben, müssen bei der Initialisierung angegeben werden. Da das Attribut \texttt{name} den Wert  \texttt{{\qq}a Thing{\qq}} als Standardwert hat, ist eine Angabe bei der Objekterzeugung optional. 

Um von Thing zu erben, ist es lediglich notwendig in der Klassendefinition Thing statt \texttt{object\%} als Superklasse anzugeben. So lässt sich beispielsweise eine Klasse namens Element definieren, die von Thing erbt:

\begin{lstlisting}
(define Element (class Thing (super-new)
                  (init-field [attr 'water])
                  (define/public (hot?) (equal? attr 'fire))))
\end{lstlisting}

Element definiert noch ein eigenes Feld sowie eine Methode, damit später das Verhalten bei Vererbung betrachtet werden kann. Objekte von Element haben sowohl Zugriff auf die neu definierten Eigenschaften als auch auf die geerbten:

\begin{lstlisting}
> (send (new Element) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am a Thing!\qq}

\begin{lstlisting}
(define elem (new Element [name "Fire"] [attr 'fire]))
> (send elem who-are-you?)
\end{lstlisting} 
{\routput {\qq}I am Fire!\qq}
\begin{lstlisting}
> (send elem hot?)
\end{lstlisting} 
{\routput{\#t}}

Element kann auch das geerbte Verhalten von Thing ändern. Sollen zum Beispiel Objekte der Klasse Element einen anderen Standardwert für den Namen haben, kann den Wert per Hand mit \texttt{init} initialisiert und dann an die Superklasse übergeben werden:

\begin{lstlisting}
(define Element (class Thing 
                  (init [name "an Element"])
                  (super-new [name name])
                  (init-field [attr 'water])
                  (define/public (hot?) (equal? attr 'fire))))
  
(send (new Element) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am an Element!\qq}

Analog lassen sich geerbte Methoden mit \texttt{define/override} überschreiben. Die Methode der Superklasse wird innerhalb einer redefinierenden Methode mit \texttt{super} aufgerufen:

\begin{lstlisting}
(define Element (class Thing 
                  ...
                  (define/override (who-are-you?)
                    (string-append (super who-are-you?)
                                   (if (hot?) " And I am hot!" "")))))
                                   
> (send (new Element) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am an Element!\qq}

\begin{lstlisting}
> (send elem who-are-you?)
\end{lstlisting}
{\routput {\qq}I am Fire! And I am hot!\qq}

Es gibt jedoch eine Einschränkung: Eigenschaften der Oberklasse sind innerhalb der Klassendefinition von Element nicht ohne weiteres sichtbar: 

\begin{lstlisting}
(define Element (class Thing 
                  ...
                  (define/public (get-name) name)))
\end{lstlisting}
{\rerror class: cannot use non-field init variable in a method in: name}

Felder und Methoden der Superklasse wurden bisher immer innerhalb eines \texttt{super}-Aufrufs benutzt. Falls jedoch ein direkter Zugriff auf den Wert von \texttt{name} innerhalb der Klassendefinition notwendig ist, so muss dies explizit durch \texttt{inherit-field} (oder \texttt{inherit} für Methoden) angegeben werden:

\begin{lstlisting}
(define Element (class Thing 
                  (init [name "an Element"])
                  (super-new [name name])
                  ...
                  (inherit-field name)
                  (define/public (get-name) name)))
\end{lstlisting}
{\rerror  class: duplicate declared identifier in: name}

Eine weitere Fehlermeldung wird ausgegeben, da zwei Deklarationen für das Feld \texttt{name} vorhanden sind: durch \texttt{init} und durch \texttt{inherit-field}. Eine der beiden Variablen muss umbenannt werden. Ein Umbenennen des Initialisierungsparamters würde bedeuten, dass sich die Syntax der Objekterzeugung ändert. Auf geerbtes Verhalten unter einem anderen Namen zugreifen zu müssen wäre jedoch sehr unintuitiv. Es wird somit das geerbte Feld umbenannt:

\begin{lstlisting}
(define Element (class Thing 
                  ...
                  (inherit-field [super-name name])
                  (define/public (get-name) super-name)))
\end{lstlisting}

Es gibt keine weitere Fehlermeldung und die Methode kann aufgerufen werden:

\begin{lstlisting}
> (send (new Element) get-name)
\end{lstlisting}
{\routput {\qq}an Element\qq}

\subsection{Mehrfachvererbung in Racket}
\label{mixins}
Im Einleitungskapitel wurde behauptet, mit Racket könne man keine Mehrfachvererbung modellieren. Tatsächlich gibt es zwei Arten von Klassen in Racket, deren Verhalten auf den ersten Blick wie Mehrfachvererbung aussieht: Mixins und Traits. Es werden deshalb beide kurz vorgestellt, um aufzuzeigen, welche Probleme und Grenzen sie haben.

Die dafür verwendete Vererbungshierarchie besteht aus der vorher definierten Klasse Thing, einen vereinfachten Version von Element sowie der folgenden Klasse Animal, die ebenfalls von Thing erbt:

\begin{lstlisting}
(define Element (class Thing 
                  (init [name "an Element"])
                  (super-new [name name])
                  (init-field [attr 'water])
                  (define/public (hot?) (equal? attr 'fire)))

(define Animal (class Thing
                 (init [name "an Animal"])
                 (super-new [name name])
                 (init-field [size 'small])))
\end{lstlisting}

Animal redefiniert ebenfalls den Standardwert für den Namen und besitzt außerdem noch ein eigenes Feld \texttt{size} für die Größe.

Animal und Element haben sowohl gemeinsame als auch unterschiedliche Felder und Methoden. Es soll versucht werden, die Felder und Methoden aus beiden in einer neuen Klasse namens Pokemon zu vereinen. 

% Das ginge natürlich ganz simpel, indem eine der beiden Klassen in Pokemon umbenannt wird und von der anderen erbt, deshalb wollen wir außerdem fordern, dass beide Klassen auch einzeln verwendet können. 


\subsubsection{Mixins}
Die Auswertung eines \texttt{class}-Aufrufs gibt liefert ein Klassenobjekt. Es ist möglich, dieses als Parameter an Funktionen oder andere Klassen zu übergeben oder auch als Rückgabewert eines Funktionsaufrufs zu definieren. Die Methode \texttt{generate-subclass} erhält beispielsweise eine Klasse als Parameter und erzeugt eine Subklasse von dieser. Dafür muss sie den Parameter nur als Superklasse in einer Klassendefinition verwenden:

\begin{lstlisting}
(define (generate-subclass superclass)
  (class superclass (super-new)))
\end{lstlisting} 

Das ist noch keine sonderlich nützliche Subklasse, da sie sich genauso verhält wie die angegebene Superklasse.
% aber wir können uns von ihrer Funktionalität überzeugen. Nehmen wir beispielsweise eine Subklasse der Klasse Element:
% 
% \begin{lstlisting}
% > (generate-subclass Element) 
% \end{lstlisting}
% {\routput \#<class:...>}
% 
% Dann erhalten wir das gleiche Verhalten wie für Objekte der Klasse Element:
% 
% \begin{lstlisting}
% > (new (generate-subclass Element))
% \end{lstlisting}
% {\routput (object:...)}
% 
% \begin{lstlisting}
% > (send (new (generate-subclass Element)) get-attr)
% \end{lstlisting}
% {\rsymbol water}
% 
Nun könnten in der in \texttt{generate-} \texttt{subclass} definierten Klasse natürlich auch noch weitere Felder und Methoden hinzugefügt werden. Die Klasse fügt dann Verhalten zu einer bestehenden, aber noch unbekannten Klasse hinzu. Erst beim Methodenaufruf wird der Platzhalter mit einer tatsächlichen Superklasse gefüllt. Eine solche Klasse wird in Racket Mixin genannt. Als Platzhalter für die Superklasse wird per Konvention \texttt{\%} genommen:

\begin{lstlisting}
(define (a-mixin %)
  (class % (super-new)
    ; neues Verhalten
    ))
\end{lstlisting}

Um das Verhalten beider Klassen Element und Animal zu vereinen, kann eine von ihnen, oder beide, als Mixin definiert werden. Beide Klassen als Mixin zu definieren kommt am nächsten an die Idee von Mehrfachvererbung:

\begin{lstlisting}
(define (Element-Mixin %)
  (class % 
    (init [name "an Element"])
    (super-new [name name])
    (init-field [attr 'water])
    (define/public (hot?) (equal? attr 'fire))))

(define (Animal-Mixin %)
  (class %
    (init [name "an Animal"])
    (super-new [name name])
    (init-field [size 'small])))
\end{lstlisting}

Und aus diesen lasseb sich dann alle drei Klassen Element, Animal und Pokemon erzeugen:
\begin{lstlisting}
(define Element (Element-Mixin Thing))

(define Animal (Animal-Mixin Thing))
 
(define Pokemon (Element-Mixin (Animal-Mixin Thing)))
\end{lstlisting}

Es fällt auf, dass die zwei Mixins nicht gleichwertig sind; der Programmierer muss entscheiden, welches von beiden zuerst angewendet werden soll. Genau genommen handelt es sich hier auch nicht um Mehrfachvererbung, sondern um zweifache Einfachvererbung -- mit dem Vorteil jedoch, dass die zwei Klassen auch unabhängig voneinander verwendet werden können. Die Hierarchie sieht also wie folgt aus:

\texttt{Pokemon $\rightarrow$ Element $\rightarrow$ Animal $\rightarrow$ Thing $\rightarrow$ object\%}

Element und Animal verhalten sich genauso wie zuvor:

\begin{lstlisting}
(send (new Animal) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am an Animal!\qq}

\begin{lstlisting}
(get-field size (new Animal))
\end{lstlisting}
{\rsymbol small}

\begin{lstlisting}
(send (new Element) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am an Element!\qq}

\begin{lstlisting}
(define elem (new Element [attr 'fire] [name "Fire"]))
> (send elem who-are-you?)
\end{lstlisting} 
{\routput {\qq}I am Fire!\qq}

\begin{lstlisting}
> (send elem hot?)
\end{lstlisting} 
{\routput \#t}

Zusätzlich ist es nun auch möglich ein Pokemon zu definieren, welche alle drei Eigenschaften aufweist:
\begin{lstlisting}
(define p (new Pokemon [name "Charmander"][size 'large][attr 'fire]))
 
> (send p who-are-you?)
\end{lstlisting}
{\routput {\qq}I am Charmander!\qq}
\begin{lstlisting}
> (send p hot?)
\end{lstlisting}
{\routput \#t}
\begin{lstlisting}
> (get-field size p)
\end{lstlisting}
{\rsymbol{large}}
\begin{lstlisting}
> (send (new Pokemon) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am an Element!\qq}

Es tritt kein Konflikt für \texttt{name} und \texttt{who-are-you?} auf. Das ist ein vorhersehbares Ergebnis, welches aus der zuvor aufgezeigten Einfachvererbung hervorgeht; das Feld \texttt{name} wird in der Vererbungshierarchie zweimal überschrieben, erst von Animal und dann von Element. Würde die Reihenfolge der Mixins vertauscht, so wäre das Ergebnis \texttt{{\qq}I am an Animal!\qq}

Wenn die letzte Ausgabe unerwünscht ist, lässt sich auch für Pokemon der Standardwert anpassen. Direkt in der Definition des Mixins ist das nicht möglich, aber die Klasse Pokemon kann stattdessen von dem resultierenden Mixin erben:

\begin{lstlisting}
(define Pokemon (class (Element-Mixin (Animal-Mixin Thing))
                  (init [name "a Pokemon"])
                  (super-new [name name])))
     
> (send (new Pokemon) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am a Pokemon!\qq}

Bisher wurden die geerbten Eigenschaften von Thing einfach immer überschrieben. Es soll daher betrachtet werden, ob die Werte von Oberklassen von Pokemon auch kombiniert werden können. Angenommen, sowohl Element als auch Animal besitzen eine \texttt{attack}-Methode. Der Wert der \texttt{attack}-Methode für Pokemon soll sich dann aus beiden Oberklassen als eine Liste aus Größe und Attribut zusammensetzen.

\begin{lstlisting}
(define (Element-Mixin %) ...
     (define/public (attack) attr)))

(define (Animal-Mixin %) ...
     (define/public (attack) size)))
\end{lstlisting}

Bei einem naiven Hinzufügen der Methode zu beiden Micins schlägt die Definition der Klasse Pokemon fehl:

{\rerror class*: superclass already contains method. method name: attack}

Racket erlaubt es nicht, dass zwei Methoden in einer Klasse den gleichen Namen haben. Element darf geerbte Methoden aus Animal nicht neu deklarieren. Falls sie überschrieben werden sollen, muss \texttt{define/override} verwendet werden:

\begin{lstlisting}
(define (Element-Mixin %) ...
    (define/override (attack) attr))
\end{lstlisting}

Das führt dazu, dass nun Thing bei der Erstellung der Elementklasse keine gültige Superklasse mehr ist, denn die Klasse Thing bietet keine Funktion \texttt{attack}, die überschrieben werden könnte. Soll es weiterhin möglich sein, Objekte von der Klasse Element zu erzeugen, so muss eine Klasse bereitgestellt werden, die eine solche Methode anbietet, damit Element von ihr erben kann.

\begin{lstlisting}
(define Element 
  (Element-Mixin (class object% (super-new)
                   (define/public (attack) null))))
\end{lstlisting}

Zudem ist der Angriff eines Pokemons lediglich der Wert des Feldes \texttt{attr}. 

Das Ziel war jedoch eine Kombination aus der Größe und dem Attribut. Das heißt, damit Pokemon das gewünschte Verhalten zeigt, muss Element eine Kombination aus dem eigenen Wert und dem der Superklasse zurückgeben.

\begin{lstlisting}
(define (Element-Mixin %) ...
    (define/override (attack) (list (super attack) attr)))
\end{lstlisting}

Abgesehen davon, dass es kein vorteilhafter Programmierstil ist, die Logik der Klasse Pokemon in eine andere Klasse auszulagern, hat es wiederum Einfluss auf Objekte der Klasse Element. Anstatt des Attributs ergibt sich nun eine etwas seltsam anmutende Liste:

\begin{lstlisting}
> (send (new Element) attack)
\end{lstlisting}
{\rsymbol (() water)}

Dafür zeigt Pokemon nun das gewünschte Verhalten.
\begin{lstlisting}
> (send p attack)
\end{lstlisting}
{\rsymbol (large fire)}

Es ist jedoch nicht mehr möglich, die Reihenfolge der Mixins zu vertauschen. Eine Definition von Pokemon als

\begin{lstlisting}
(define Pokemon (Animal-Mixin (Element-Mixin Thing)))
\end{lstlisting}

führt zu einem Fehler, aus dem gleichen Grund wie vorher bei Objekten der Klasse Element: da Element eine Superklasse erwartet, die eine Funktion \texttt{attack} anbietet. Da Thing nun die direkte Superklasse ist, ist das nicht mehr der Fall. Selbst wenn das behoben wäre, würde die Erzeugung nun an Animal scheitern, da es die geerbte Methode aus Element nicht überschreibt, sondern versucht neu zu definieren. Es müssten alle Schritte, die soeben zur erfolgreichen Vererbung von der Methode \texttt{attack} durchgeführt wurden, auch für den umgekehrten Fall durchgeführt werden. 

Falls beide Vererbungsreihenfolgen erlaubt sein sollen, wird zudem die Funktion \texttt{attack} deutlich komplizierter, da nun anhand der Superklasse entschieden werden müsste, ob der Wert des Feldes direkt zurückgegeben werden kann oder eine Kombination mit dem Wert der Superklasse nötig ist.

Bisher wurden nur zwei Superklassen betrachtet. Bei drei, vier oder gar 20 Superklassen, die vielleicht selbst wiederum von mehreren Superklassen erben, wird, falls eine sinnvolle Modellierung von Methodenkombination mit Mixins überhaupt noch möglich ist, der Code extrem unübersichtlich und wartungsunfreundlich. Insbesondere für die Lehre eignen sie sich demnach nicht.

\subsubsection{Traits}
Traits sind ähnlich zu Mixins. Sie kapseln eine Menge von Methoden, die zu einer Klasse hinzugefügt werden sollen. Traits erlauben jedoch Kontrolle darüber, welche Methoden wie geerbt werden. Es ist möglich bestimmte Methoden nicht zu erben, sie unter einem Alias zu erben oder mit Traitoperatoren zu manipulieren und die Ergebnisse mehrerer Methoden zu kombinieren.

Sie lösen damit eines der fundamentalen Probleme von Mixins: Die Vererbung und Kombination gleichbenannter Methoden. Wenn es in zwei Traits, die kombiniert werden sollen, gleichbenannte Methoden gibt, so hat der Programmierer die Möglichkeit (und Pflicht), anzugeben, wie diese Kollision gelöst werden soll -- üblicherweise durch Ausschließen oder Umbenennen einer der Methoden in der Subklasse, oder durch Methodenkombination.

Die Definition von Traits ist syntaktisch oft fast identisch zu der Definition einer Klasse, es gibt nur zwei Unterschiede: Anstelle des Schlüssworts \texttt{class} wird \texttt{trait} benutzt und es müssen (und können) weder Superklasse noch Superkonstruktoraufruf angegeben werden. Traits unterstützen einen Großteil der Optionen, die auch \texttt{class} unterstützt. Unter anderem sind jedoch weder \texttt{init}- noch \texttt{init-field}-Deklarationen erlaubt. Das bedeutet, dass ein Zugriff auf \texttt{name} nicht möglich ist (der entsprechende Code entfällt daher) und für die Definition von Feldern nur die Option \texttt{field} zur Verfügung steht.

\begin{lstlisting}
(require racket/trait)

(define Element-Trait
  (trait (field [attr 'water]) ; statt init-field
         (define/public (hot?) (equal? attr 'fire))
         (define/public (attack) attr)))

(define Animal-Trait
  (trait (field [size 'small])
         (define/public (attack) size)))
\end{lstlisting}

Um einen Pokemontrait aus diesen beiden Traits zu definieren, muss den Konflikt der beiden \texttt{attack}-Methoden behoben werden. Das geht jedoch im Gegensatz zu Mixins direkt im Pokemon-Trait. Zur Manipulation der Vererbung gibt es verschiedene Trait-Operationen, wie
\begin{itemize}
 \item \texttt{trait-exclude}, das eine Methode von einem Trait entfernt,
 \item \texttt{trait-alias}, welches die Kopie einer Methode unter anderem Namen zum Trait hinzufügt und
 \item \texttt{trait-sum}, welches die Methoden von zwei Traits kombiniert.
\end{itemize}

Das Vorgehen bei einem Konflikt lässt sich generalisieren. Zunächst wird dafür gesorgt, dass es keinen Namenskonflikt mehr gibt. Dafür wird aus den zwei Traits mit kollidierender Methode jeweils ein neuer Trait erstellt, in dem diese Methode einen neuen, eindeutigen Namen erhält. Dafür würde beispielsweise zum Trait Element mit \texttt{trait-alias} einen Alias namens \texttt{element-attack} für die \texttt{attack}-Methode hinzugefügt. Der Trait hat anschließend \emph{zwei} Funktionen für den Angriff, \texttt{attack} und \texttt{element-attack}, die beide das gleiche tun. Anschließend lässt sich mit \texttt{trait-exclude} die ursprüngliche \texttt{attack}-Methode von der Vererbung ausschließen. Es wird also nur die Aliasmethode \texttt{element-attack} vererbt.

Dieser Schritt wird für jeden Konflikt durchgeführt. Sobald alle Methoden einen eindeutigen Namen haben, können sie mit \texttt{trait-sum} kombiniert werden.

\begin{lstlisting}
 (define Pokemon-Trait
   (trait-sum   ; combine the following traits
    (trait-exclude (trait-alias Element-Trait   ; create an alias for
                                attack          ; attack and remove
                                element-attack) ; the original
                   attack)
    (trait-exclude (trait-alias Animal-Trait    ; same for animal
                                attack         
                                animal-attack)
                   attack)
    (trait (inherit element-attack animal-attack) ; combine the two
           (define/public (attack)                ; attack methods
             (list (animal-attack) (element-attack))))))
\end{lstlisting}

Das Ergebnis ist ein neuer Trait. 

\begin{lstlisting}
> Pokemon-Trait
\end{lstlisting}
{\routput \#<trait>}

Traits können nicht direkt zu Klassen hinzugefügt werden, aber sie lassen sich mit der Funktion \texttt{trait->mixin} in ein Mixin umwandeln und dieses kann zu einer Klasse hinzufügt werden:

\begin{lstlisting}
 (define Pokemon-Mixin (trait->mixin Pokemon-Trait))
 (define Pokemon (Pokemon-Mixin Thing))
\end{lstlisting}

Da \texttt{field} statt \texttt{init-field} bei der Traitdefinition benutzt wurde, ist es nun jedoch nicht mehr möglich, die Felder zu initialisieren. Soll dies trotzdem möglich sein, so muss, wie zuvor bei dem Mixin, eine Subklasse erzeugt und alle Felder per Hand initialisiert werden:

\begin{lstlisting}
(define Pokemon (class (Pokemon-Mixin Thing)
                  ; add initialization arguments by hand
                  (init [name "a Pokemon"]
                        [size 'small]
                        [attr 'water])
                  ; name already is an init-field, we can just
                  ; pass it to the super call
                  (super-new [name name])
                  ; to have access to the others, we first need
                  ; to inherit them
                  (inherit-field [super-size size] [super-attr attr])
                  ; and then we can set them to the desired value
                  (set! super-size size)
                  (set! super-attr attr)))
\end{lstlisting}

Das gleiche Vorgehen muss analog für die Klassen Element und Animal angewendet werden, falls sie auch einzeln verwendet werden. Die Klasse Pokemon zeigt die erwarteten Eigenschaften:

\begin{lstlisting}
> (send (new Pokemon) who-are-you?)
\end{lstlisting}
{\routput {\qq}I am a Pokemon!\qq}

\begin{lstlisting}
(define p (make-object Pokemon "Charmander" 'large 'fire))
> (send p who-are-you?)
\end{lstlisting}
{\routput {\qq}I am Charmander!\qq}

\begin{lstlisting}
> (send p hot?)
\end{lstlisting}
{\routput \#t}

\begin{lstlisting}
> (send p attack)
\end{lstlisting}
{\rsymbol (large fire)}

Die Konfliktlösung bei Traits ist für einen ersten Einblick in Mehrfachvererbung recht umständlich und die Lösung für die Initialisierungsparameter per Hand ist aufwendig. Zusätzlich sind Traits schlussendlich bessere Mixins und auch sie werden bei komplizierteren Vererbungshierarchien schnell unübersichtlich.

\subsection{Ergänzungsmethoden}

Es wurde bereits erwähnt, dass sich neue Methoden als öffentlich (\texttt{define/public}) oder privat (\texttt{define/private}) einführen lassen und geerbte Methoden aus der Superklasse mittels \texttt{define/override} überschrieben werden können. Racket bietet darüber hinaus eine Reihe von Möglichkeiten, die Vererbung von Methoden zu beeinflussen.

Zusätzlich zum Überschreiben von Methoden erlaubt Racket es, Methoden zu ergänzen. Die angegebene Klassenoption nach \texttt{define/} bestimmt, ob die definierte Methode eine geerbte Methode überschreibt oder ergänzt und auch, ob es möglich ist, diese Methode in einer Subklasse zu überschreiben oder zu ergänzen (Tabelle \ref{methods}).

\begin{table}[h]
 \centering \small
\begin{tabular}{|c'c|c|c|c|c}
 \hline
		& \multicolumn{1}{p{2.2cm}|}{\centering überschreibt geerbte Methode} 
		& \multicolumn{1}{p{2.2cm}|}{\centering erweitert geerbte Methode}
		& \multicolumn{1}{p{2.2cm}|}{\centering überschreibbar}
		& \multicolumn{1}{p{2.2cm}|}{\centering erweiterbar}
		\\ \thickhline
%                 & overrides & augments & can be     & can be    \\
%                 & method    & method   & overridden & augmented \\ \thickhline
 public         &           &          &     x      &           \\ \hline
 pubment        &           &          &            &    x      \\ \hline
 public-final   &           &          &            &           \\ \hline
 override       &     x     &          &     x      &           \\ \hline
 overment       &     x     &          &            &    x      \\ \hline
 override-final &     x     &          &            &           \\ \hline
 augride        &           &    x     &     x      &           \\ \hline
 augment        &           &    x     &            &    x      \\ \hline
 augment-final  &           &    x     &            &           \\ \hline
\end{tabular}
\caption{Methodenarten in Object-Racket}
\label{methods}
\end{table}

Wird eine geerbte Methode mittels einer der drei \texttt{define/over*} Optionen überschrieben, so kann im Methodenrumpf mit \texttt{super} die Methode der Superklasse aufgerufen werden. Das erlaubt es, das Ergebnis der Supermethode weiter zu verwenden, zum Beispiel als Teil einer Berechnung:

\begin{lstlisting}
(define TheNumber (class object% (super-new)
                     (define/public (number)
                       (display "The number is ")
                       23)))

(define Sub (class TheNumber (super-new)
              (define/override (number)
                (display "Actually ")
                (+ (super number) 19))))

(send (new TheNumber) number)
\end{lstlisting}
{\routput The number is 23}
\begin{lstlisting}
(send (new Sub) number)
\end{lstlisting}
{\routput Actually The number is 42}

Um eine Methode zu ergänzen, muss sie in der Superklasse mit einer Methode des Schemas \texttt{define/*ment} definiert sein. Sie lässt sich dann mit einer Methode des Schemas \texttt{define/aug*} um Funktionalität ergänzen. Die Ergänzung erfolgt nach dem Prinzip von Schablonen- und Einschubmethode. Die Oberklasse gibt innerhalb der Methode durch einen Aufruf von \texttt{inner} an, an welcher Stelle die Methode der Subklasse aufgerufen werden soll (Schablonenmethode). Die erbende Klasse implementiert diese Methode (Einschubmethode):

\begin{lstlisting}
(define TheNumber (class object% (super-new)
                  (define/pubment (number)
                    (inner (void) number)
                    (display "The number is ")
                    23)))

(define Sub (class TheNumber (super-new)
                    (define/augment (number)
                      (display "Believe it! "))))
                      
> (send (new TheNumber) number)
\end{lstlisting}
{\routput The number is 23}

\begin{lstlisting}                
> (send (new Sub) number)
\end{lstlisting}
{\routput Believe it! The number is 23}

Das erste Argument von \texttt{inner} ist der Standardwert. Er wird ausgewertet, wenn es keine ergänzende Methode gibt. In diesem Beispiel soll nichts geschehen. Das zweite Argument ist der Name der ergänzenden Methode. Er muss mit dem Funktionsnamen übereinstimmen. Anschließend können noch Funktionsargumente übergeben werden. Der \texttt{inner}-Aufruf kann prinzipiell an einer beliebigen Stelle im Methodenrumpf stehen. Falls die Methode einen Wert zurückgibt, so muss dies nach dem letzten \texttt{inner}-Aufruf geschehen. Es ist möglich, mehrere \texttt{inner}-Aufrufe zu platzieren, dann wird die Methode der Subklasse mehrmals ausgeführt. 

Liefert die Subklasse einen Rückgabewert, so kann dieser weiter verwendet werden:

\begin{lstlisting}
(define TheNumber (class object% (super-new)
                     (define/pubment (number)
                       (display "The number is ")
                       (display (inner 23 number)))))

(define Sub (class TheNumber (super-new)
              (define/augment (number)
                42)))

> (send (new TheNumber) number)
\end{lstlisting}
{\routput The number is 23}

\begin{lstlisting}
> (send (new Sub) number)
\end{lstlisting}
{\routput The number is 42}

Durch \texttt{overment} ist eine Methode in der Lage, gleichzeitig das Ergebnis der Superklasse und das Ergebnisse der Subklasse abzufragen und weiterzuverwenden. In einer größeren Vererbungshierarchie können  lange Ketten von sich überschreibenden und ergänzenden Methoden entstehen. Je abwechslungsreicher die verwendeten Methodenoptionen in einer Vererbungshierarchie sind, desto schwieriger kann es sein nachzuvollziehen, was die effektive Methode für ein Objekt der untersten Subklasse in welcher Reihenfolge bewirkt.

Es wurde gezeigt, dass Object-Racket keine direkte Möglichkeit für Mehrfachvererbung bietet. Es gibt Mixins und Traits, doch beide werden intern auf Einfachvererbung abgebildet und damit für kompliziertere Vererbungshierarchien schnell unübersichtlich. Dafür gibt es sehr umfangreiche Möglichkeiten, die Redefinition und Erweiterung von Methoden zu kontrollieren. Dabei sind alle  Methodenoptionen kompatibel mit Mixins und Traits. 

