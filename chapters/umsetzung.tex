\chapter{Umsetzung}  
\label{implementation}
Im Rahmen dieser Arbeit ist ein Prototyp entstanden, das das \texttt{class}-Makro um Mehrfachvererbung und Methodenkombination erweitert. Ein Makro nimmt die übergebene Syntax, hält wichtige Informationen in Form von Metaobjekten fest und generiert schließlich die Syntax, die vom eigentlichen \texttt{class}-Makro ausgewertet wird.

\section{eval im Defintionsfenster}

Das erste Problem, das sich dabei stellt, ist, dass Racket es nicht erlaubt, mehrere Superklassen anzugeben. Wir können zwar ein Makro schreiben, das mehrere Superklassen akzeptiert, aber der Makroaufruf, zu dem schlussendlich expandiert wird, darf nur eine Superklasse beinhalten. Das bedeutet, wir müssen aus den angegebenen Klassen selbst eine Superklasse erzeugen, die alle Eigenschaften der Klassen in sich vereinigt und diese als offizielle Superklasse angeben. Wie genau das funktioniert, sehen wir später, aber wir werden dafür die Klassenoptionen ebenjener Superklasse zusammenbauen, in einen \texttt{class}-Aufruf stecken und dann auswerten müssen.

\texttt{class} ist jedoch ein Makro, keine Funktion. Wir können es nicht einfach mit \texttt{apply} auf eine von uns zusammengestellte Liste von Klassenoptionen anwenden. Die andere naheliegende Option ist \texttt{eval}, welche jedoch nicht ohne weiteres außerhalb der Interaktionskonsole benutzt werden kann. Das Bereitstellen von \texttt{eval} auch innerhalb des Definitionsfensters geschieht noch vor der eigentlichen Expansion des Makros, deshalb soll auch hier zuerst darauf eingegangen werden.

\texttt{eval} ist eine Funktion in Racket, die einen quotierten Ausdruck nimmt und ihn auswertet. Salopp gesagt entfernt die Funktion ein Quotierungszeichen (wenn es eins gibt):

\begin{lstlisting}
> (define x 42)
> 'x
\end{lstlisting}
{\rsymbol x}

\begin{lstlisting}
> (eval 'x)
\end{lstlisting}
{\routput 42}

Wir können damit insbesondere einen quotierten Funktionsaufruf auswerten:

\begin{lstlisting}
> (eval '(+ 1 2))
\end{lstlisting}
{\routput 3}

Aber \texttt{eval} funktioniert ohne weiteres nur in der Interaktionskonsole. Versuchen wir im Definitionsfenster den gleichen Ausdruck auszuwerten, so erhalten wir einen Fehler.

{\color{red}\ttfamily\small\hspace{5pt} +: unbound identifier;}\\
{\color{red}\ttfamily\small\hspace{5pt} also, no \#\%app syntax transformer is bound in: +}\\

\texttt{eval} kann die Bindungen des Kontextes, in dem es aufgerufen wird, nicht sehen. Das beinhaltet auch alle importierten Racket-Funktionen, selbst \texttt{+}. Damit \texttt{eval} Zugriff auf die Umgebung hat, in der es aufgerufen wird, muss man einen Namescpace mit übergeben. In der Interaktionskonsole passiert das automatisch; im Definitionsfenster müssen wir es per Hand machen. Dafür kann als zweiter Parameter ein Namespace-Wert übergeben werden. 

Die folgenden Definitionen und Beispiele sind dem Racket-Guide \cite{racketguide-namespace} entnommen.

Ein Namespace beinhaltet zwei Informationen:
\begin{enumerate}
 \item Eine Abbildung von Schlüsselwörtern zu Bindungen (zum Beispiel der Name \texttt{+} zu der Funktion \texttt{+}). Ein leerer Namespace bildet jeden Namen auf eine unintialisierte Variable ab.
 \item Eine Abbildung von Modulnamen zu Moduldeklarationen und Instanzen.
\end{enumerate}

Die erste Abbildung ist notwendig, zum Ausdrücke wie das Beispiel der Addition auszuwerten; die zweite für die Auwertung von \texttt{require}-Befehlen.

Ein leerer Namespace kann mit der Funktion \texttt{make-empty-namespace} erzeugt werden. Da der Namespace jedoch leer ist, passiert das gleiche wie als wenn wir keinen Namespace angegeben hätten: die Auswertung schlägt fehl. Um einen Namespace benutzbar zu machen, müssen Module aus dem aktuellen Namespace angehängt werden. \texttt{make-base-empty-namespace} erzeugt beispielsweise einen Namespace, an den das Modul \texttt{racket/base} bereits angehängt ist. Andere Module lassen sich mir \texttt{namespace-attach-module} anhängen:

\begin{lstlisting}
(namespace-attach-module (current-namespace)
                         'racket/class
                         (make-base-empty-namespace))
\end{lstlisting}

\texttt{current-namespace} liefert den aktuellen Namespace. Er beinhaltet alle Bindungen, die innerhalb des Kontextes, in dem die Funktion aufgerufen wird, vorhanden sind. Das \texttt{racket/class}-Modul aus dem aktuellen Namespace wird an den leeren Base-Namespace angehängt.

Das Anhängen eines Moduls bewirkt, dass innerhalb des Namespaces nun eine Abbildung des Modulnamen existiert; Mappings für die Funktionen und Schlüsselwörter existieren noch nicht. Wir haben dem Namespace lediglich mitgeteilt, wo das Modul zu finden ist. Für das Importieren gibt es die Funktion \texttt{parameterize}:

\begin{lstlisting}
(parameterize ([current-namespace (make-base-empty-namespace)])
  (namespace-require 'racket/class))
\end{lstlisting}

Module, die nicht dem Namespace angehängt sind, wie hier \texttt{racket/class}, werden neu geladen und instanziiert. Das bedeutet, dass für den Namespace ein neuer, eigenständiger \texttt{class}-Datentyp erzeugt wird:

\begin{lstlisting}
> (require racket/class)
> (class? object%)
\end{lstlisting}
{\routput \#t}

\begin{lstlisting}
> (class?
   (parameterize ([current-namespace (make-base-empty-namespace)])
     (namespace-require 'racket/class) ; loads again
     (eval 'object%)))
\end{lstlisting}
{\routput \#f}

Falls die gleiche Instanz des Datentyps benutzt werden soll, so muss das Modul zuerst an den Namespace angehängt werden:

\begin{lstlisting}
> (require racket/class)
> (class?
   (let ([ns (make-base-empty-namespace)])
     (namespace-attach-module (current-namespace)
                              'racket/class
                              ns)
     (parameterize ([current-namespace ns])
       (namespace-require 'racket/class) ; uses attached
       (eval 'object%))))
\end{lstlisting}
{\routput \#f}

Benötigt man den Namespace nur innerhalb eines Moduls, so kann man einen Name\-space-An\-chor definieren. Für das Benutzen von \texttt{eval} im Definitionsfenster können wir beispielsweise einfach schreiben:

\begin{lstlisting}
(define-namespace-anchor anchor)
(define ns (namespace-anchor->namespace anchor))
(define (my-eval x) (eval x ns))

> (my-eval '(+ 1 2))
\end{lstlisting}
{\routput 3}

Unser Modul definiert jedoch ein Makro, das in einem anderen Modul expandiert wird; und somit auch in einem anderen Namespace. Wenn wir auf diesen Namespace innerhalb unseres Moduls zugreifen wollen, so müssen wir die Funktion \texttt{my-eval} innerhalb der Makroexpansion definieren. Dafür wird die vorhergehende Methode mit \texttt{namespace-attach-module} und \texttt{parameterize} benutzt. Da wir später in der eigentlich Expansion des \texttt{class}-makros drei Fälle unterscheiden wollen, passiert diese ``Zwischenexpansion'' in einem eigenen Makro:

\begin{lstlisting}
(define-syntax my-class
  (syntax-rules ()
    ; Match (my-class ...)
    [(my-class arg . args)
     ; Create a new namespace.
     (let ([ns (make-base-namespace)])
       ; Attach modules of the current namespace.
       (namespace-attach-module (current-namespace) 'racket/class ns)
       (namespace-attach-module (current-namespace) 'racket/list ns)
       (namespace-attach-module (current-namespace) 'racket/function ns)
       ; Import identifiert bindings from attached modules.
       (parameterize ([current-namespace ns])
         (namespace-require 'racket/class)
         (namespace-require 'racket/list)
         (namespace-require 'racket/function))
       ; Create an eval function that uses the namespace
       ; and add it to the syntax of the class call.
       ; Then expand the original class macro.
       (my-eval-class (lambda (x) (eval x ns)) arg . args))]))
\end{lstlisting}

Das Makro matcht Aufrufe von \texttt{my-class}. Wir erzeugen einen neuen Namespace, hängen dann die im Kontext des Makroaufrufes gebundenen Module \texttt{racket/class}, \texttt{racket/list} und \texttt{racket/function} an und binden sie anschließend ein. Der resultierende Namespace wird für die Definition einer \texttt{eval}-Funktion benutzt, die zu der Syntax des \texttt{my-class}-Aufrufes hinzugefügt wird. Anschließend wird das Makro \texttt{my-eval-class} aufgerufen, das die eigentliche Expansion übernimmt.

Der Name des Makros ist \texttt{my-class}, damit wir intern weiterhin Racket-Klassen mittels \texttt{class} erzeugen können. Wir benennen das Makro erst beim Export um:

\begin{lstlisting}
(provide (rename-out [my-class class]))
\end{lstlisting}

\section{Redefinition des class-Makros}
Im Makro \texttt{my-eval-class} geschieht die eigentliche Expansion des \texttt{class}-Makros. Es werden drei Fälle nach Syntax der Superklasse unterschieden, je nachdem welche Superklassen angegeben wurden:
\begin{itemize}
 \item eine einzelne Superklasse 
 \item die leere Liste \texttt{()} - wir interpretieren sie als \texttt{object\%}
 \item eine Liste von Superklassen \texttt{($<$superclass1$>$ $<$superclass2$>$ ...)}
\end{itemize}

Bei allen drei Optionen wird die Funktion \texttt{expand!} mit der \texttt{my-eval}-Funktion, der Liste der Superklassen und der Liste der restlichen Klassenoptionen aufgerufen:

\begin{lstlisting}
(define-syntax my-eval-class
  (syntax-rules ()
    [(my-eval-class my-eval () . rest) 
     (expand! my-eval (list object%) 'rest)]
    [(my-eval-class my-eval (super ...) . rest)
     (expand! my-eval (list super ...) 'rest)]
    [(my-eval-class my-eval super . rest)
     (expand! my-eval (list super) 'rest)]))
\end{lstlisting}

In \texttt{expand!} wird analog zu CLOS zu der Klassendefinition ein Metaobjeckt und ein Klassenobjekt erzeugt, die Klasse zur Liste der beobachteten Klassen hinzugefügt und anschließend das Klassenobjekt zurückgegeben.

\begin{lstlisting}
(define (expand! my-eval supers args)
  (let* ([meta (ensure-class supers args)]
         [obj  (make-classobject my-eval supers args meta)])
    (add-class obj meta)
    obj))
\end{lstlisting}

Da Klassen in Racket jedoch keine Namen haben, wird das Objekt selbst als Schlüssel in der Klassenliste benutzt. \texttt{ensure-class} hat die gleiche Funktionalität wie in der CLOS-Implentierung. Sie hält alle wichtigen Informationen über das gerade zu erzeugende Objekt fest. 

\begin{lstlisting}
(define (ensure-class supers args)
  (let* ([direct-supers     (map find-class supers)]  
         [direct-fields     (compute-direct-fields args)]
         [direct-methods    (filter method-definition? args)]
         [generic-functions (filter generic-function-definition? args)]
         [meta (make-object meta-class% direct-supers direct-fields
                 direct-methods generic-functions)])
    (finalize-inheritance meta)
    (ensure-generic-functions meta)
    meta))
\end{lstlisting}


Das passiert in drei Schritten:
\begin{itemize}
 \item Es wird ein Metaobjekt mit den angegebenen Superklassen, Feldern, Methoden und generischen Funktionen erzeugt.
 \item \texttt{finalize-inheritance} fügt die Klassenpräzedenzliste, geerbte Felder und Methoden hinzu.
 \item \texttt{ensure-generic-functions} aktualisiert die Liste der generischen Funktionen. Neue generische Funktionen, die in der aktuellen Klasse definiert werden, werden hinzugefügt und Methoden, zu denen eine generische Funktion existiert, werden zu der generischen Funktion hinzugefügt.
\end{itemize}

Das erzeugte Klassen-Metaobjekt wird anschließend zurückgegeben, denn wir müssen noch das tatsächliche Klassenobjekt erzeugen, bevor wir es zur Liste hinzufügen können. Die Erzeugung des Klassenobjekts betrachten wir zum Schluss. Zunächst soll auf die Implementierung der Metaklassen für Klassen und generische Funktionen näher eingegangen werden.

\section{Klassen-Metaobjekte}
Klassen-Metaobjekte werden analog zu CLOS definiert. Es gibt eine interne Racket-Klasse für Metaobjekte, hier \texttt{meta-class\%} genannt, die sich alle wichtigen Informationen über Superklassen, Felder und Methoden in Form von Feldern merkt.

Im Gegensatz zu CLOS manipulieren wir jedoch nur Syntax. Anstelle von Feld- und Methodenobjekten merkt sich die Metaklasse die Definitionen der Felder und Methoden. Sie werden erst ganz am Ende von Racket ausgewertet werden, als Teil der Definition des Klassenobjekts, das an den Nutzer zurückgegeben wird.

Für die Klasse Thing aus dem Object-Racket-Kapitel

\begin{lstlisting}
(define Thing (class object% (super-new)
                (init-field [name "a Thing"])
                (define/public (who-are-you?) 
                  (string-append "I am " name "!"))))
\end{lstlisting}

würden wir als direkte Felder die Liste 

\texttt{{\textquotesingle}((init-field [name {\qq}a Thing\qq]))} 

und als direkte Methoden die Liste 

\texttt{{\textquotesingle}((define/public (who-are-you?) (string-append {\qq}I am {\qq} name \qq!\qq)))} 

festhalten. Eine Ausnahme bilden die Superklassen. Klassen sind in Racket nicht benannt und der Benutzer hat die Freiheit die gleiche Klasse mehreren Variablen zuzuweisen. Um eine Klasse eindeutig identifizieren zu können, brauchen wir also die Referenz; wir müssen die Superklassen auswerten. Wir werden uns später nicht für die eigentlichen Superklassen, sondern für die zugehörigen Metaobjekte interessieren. Deshalb halten wir in der Klassenpräzedenzliste die Präzedenz der Metaklassen fest.

Um Klassen für Debugging unterscheiden zu können, weisen wir jeder Klasse außerdem eine aufsteigende Nummer zu. Die Anzahl der bisher erzeugten Klasse wird in der Variable \texttt{num-of-classes} festgehalten.

Im Gegensatz zu CLOS werden am Ende der Expansion von einer Klasse mit mehreren Superklassen zwei Klassen erstellt: Die Klasse, die die Eigenschaften der Superklassen zusammenfasst und die neue, durch das Makro definierte, Klasse, die von der zusammengestellten Superklasse erbt. Das bedeutet, dass wir am Ende unterscheiden zwischen geerbten Feldern und Methoden und solchen, die in der neuen Klasse definiert wurden. Es bietet sich daher an, anstelle der effektiven Felder und Methoden nur die geerbten festzuhalten. Die effektiven Eigenschaften lassen sich leicht in einer Methode als Vereinigung von direkten und geerbten ermitteln; umgekehrt müssten wir jedesmal die Liste der effektiven Eigenschaften filtern, um nur die geerbten zu erhalten. 

Es ergibt sich demnach die folgende Defintion für die Klassen-Metaklasse:

\begin{lstlisting}
(define meta-class%
  (class object% (super-new)
    (init-field direct-supers
                direct-fields
                direct-methods
                generic-functions)
    (field [number (begin 
                     (set! num-of-classes (+ 1 num-of-classes))
                     num-of-classes)]
           [class-precedence-list '()]
           [inherited-fields '()]
           [inherited-methods '()]
           [applicable-generic-functions '()])    
    (define/public (effective-fields)
      (append direct-fields inherited-fields))
    (define/public (effective-methods)
      (append direct-methods inherited-methods))))
\end{lstlisting}

In einer Hashtabelle werden die bereits erzeugen Klassen als Paar (Klassenobjekt, Metaobjekt) festgehalten und es gibt die Methoden \texttt{findclass} und \texttt{add-class}, die den Zugriff erleichtern. Zu Beginn befindet sich bereits \texttt{object\%} und sein Metaobjekt in der Tabelle.

Die Superklassen, direkten Felder und Methoden lassen sich aus der Syntax der Klassendefinition extrahieren. Um Felder später unabhängig voneinander vererben zu können, werden \texttt{field}- und \texttt{init-field}-Optionen, die mehr als ein Feld definieren, gesplittet. Aus

\texttt{(field [a 1] [b 2])}

wird beispielsweise zu

\texttt{(field [a 1]) (field [b 2])}

umgewandelt. Die Methode \texttt{compute-direct-fields} übernimmt diese Konversion.

Die Eigenschaften, die sich durch Einfach- oder Mehrfachvererbung ergeben, werden durch die Methode \texttt{finalize-inheritance} zu dem Metaobjekt hinzugefügt. Sie berechnet die Klassenpräzedenzliste und anhand dieser die geerbten Felder und Methoden und bestückt die entprechenden Felder des Metaobjekts.

Für die Berechnung der Klassenpräzedenzliste wird die Funktion \texttt{compute-std-cpl} aus der CLOS-Implementierung im Racket-Paket \texttt{swindle} verwendet. Sie implementiert die Klassenpräzedenzliste bereits nach den in AMOP definierten Regeln. Die Funktion erhält das Objekt, für das die Klassenpräzedenzliste berechnet werden soll (wir verwenden das zugehörige Metaobjekt) sowie eine Funktion, die zu einem (Meta-)Objekt die direkten Superklassen zurückgibt -- und liefert uns die vollständige Klassenpräzedenzliste. Die Funktion befindet sich in \texttt{tinyclos.rkt}, wird jedoch nicht exportiert. Der Quelltext wurde in unser Modul kopiert, damit Benutzer des Moduls nicht erst einmal \texttt{tinyclos.rkt} um einen Exportbefehl ergänzen müssen. 

Die Methoden \texttt{compute-inherited-fields} und \texttt{compute-inherited-methods} nutzen dann die Klassenpräzedenzliste, um die geerbten Felder und Methoden zu berechnen. Sie führen damit insbesondere die \textit{standard method combination} (ohne Ergänzungsmethoden) durch.

Für jedes Objekt in der Klassenpräzedenzliste sammeln sie die dort definierten direkten Eigenschaften und fügen diese zu einer Liste zusammen. Anschließend werden Duplikate, also Felder und Methoden mit gleichem Namen, aus der Liste entfernt. Nur das erste Auftreten, das Feld oder die Methode der Klasse mit der höchsten Präzedenz, verbleibt in der Liste. Da das Vorgehen bei Methoden und Feldern sich nur geringfügig unterscheidet, wird die Logik in der Methode \texttt{compute-inherited-stuff} zusammengefasst. Der Parameter \texttt{direct-stuff} ist entweder \texttt{{\textquotesingle}direct-fields} oder \texttt{{\textquotesingle}direct-methods}:

\begin{lstlisting}
(define (compute-inherited-stuff direct-stuff cpl)
  (let ([stuff '()])
    ; add the fields/methods of all superclasses to the list
    (for*/list ([metas cpl])
      (set! stuff (append stuff 
                          (dynamic-get-field direct-stuff metas))))
    ; only keep the first appearance of every field/method declaration
    (filter-first-occurence stuff)))
\end{lstlisting}

\texttt{filter-first-occurance} sorgt für die Entfernung von Duplikaten. Aus

\texttt{\textquotesingle((field [a 1]) (field [a 2]) (field [b 3]) (field [b 0]))}

wird beispielsweise

\texttt{\textquotesingle((field [a 1]) (field [b 3]))}.

Mit der bisher eingefüherten Funktionalität können wir bereits alle notwendigen Informationen für das Klassen-Metaobjekt sammeln.

\section{Generische-Funktionen-Metaobjekte}
Für generische Funktionen gibt es ebenfalls eine Struktur aus Metaobjekten. Die Metaklasse für generische Funktionen heißt \texttt{meta-generic-function\%} und merkt sich für eine generische Funktion den Namen, die Parameterliste, das Klassen-Metaobjekt, in der sie definiert wurde, die Methodenkombination sowie die Liste der Syntax von implementierenden Methoden. Bis auf die Liste der implementierdenden Methoden können all diese Informationen aus der Syntax der Klassendefinition abgeleitet werden.

Die implementierenden Methoden werden in der Form (Metaklasse, Methodendefintion) in einer Hashtabelle abgelegt. Die Verwaltung dieser Tabelle wird ebenfalls von der Generische-Funktionen-Metaklasse übernommen. Sie bietet Methoden zum Hinzufügen einer implementierenden Methode sowie zur Berechnung der anwendbaren Methoden für ein Klassen-Metaobjekt.

Die anwendbaren Methoden (\emph{applicable methods}) werden anhand der Klassenpräzedenzliste des Klassenmetaobjektes bestimmt: die Liste der Methoden wird nach denjenigen Methoden gefiltert, die auf ein Objekt aus der Klassenpräzedenzliste spezialisiert sind. Falls keine anwendbaren Methoden existieren, so wird eine Error-Methode zurückgegeben. 

Würde der Fehler direkt an dieser Stelle geworfen, würden wie den Benutzer zwingen, für jede Klasse, die an der Methodenkombination beteiligt sein kann, auch eine implementierende Methode anzubieten. Eine Hierarchie wie im Pokemon-Beispiel, wo die Klasse Thing lediglich die generische Funktion \texttt{attack} definiert ohne eine implementierende Methode anzubieten, wäre nicht möglich. Wir wollen dieses Verhalten jedoch erlauben. Der Fehler soll erst auftreten, wenn der Benutzer tatsächlich versucht, die generische Funktion an einem Objekt der Klasse Thing aufzurufen. Das erreichen wir, indem die Methode, die den Fehler wirft, das Ergebnis der Methodenkombination ist.

Es ergibt sich damit folgende Defintion für die Generische-Funktionen-Metaklasse:

\begin{lstlisting}
(define meta-generic-function%
  (class object% (super-new)
    (init-field name
                params
                meta-class
                combination) 
    (field [methods (make-hasheq)]) 
    (define/public (number-of-params) (length params))
    (define/public (add-method meta method) 
                     (hash-set! methods meta method))
    (define/public (applicable-methods meta) ... )))
\end{lstlisting}

Analog zu Klassen gibt es auch für generische Funktionen eine Tabelle mit dem Schema (Name, Generische-Funktion-Metaobjekt) und entsprechenden Zugriffsfunktionen \texttt{add-generic}, \texttt{get-generic} und \texttt{is-generic?}.

Die Verwaltung der Liste geschieht durch die Funktion \texttt{ensure-generic-functions}. 

Sie erhält das frisch erzeugte Metaobjekt zu einer Klasse. Falls in der Klasse generische Funktionen definiert werden, so werden sie zu der Liste hinzugefügt. Ist die generische Funktion bereits vorhanden, so wird ein Fehler signalisiert.

Anschließend werden die Methodendefinitionen verarbeitet. Wie bei Klas\-sen-Me\-ta\-ob\-jek\-ten werden auch Methoden und generische Funktionen über die Syntax ihrer Definition repräsentiert. Da wir für Methoden darüber hinaus keine weiteren Informationen benötigen, gibt es keine eigene Metaklasse. 

Falls eine generische Funktion für eine Methode existiert, die auf eine Klasse in der Präzedenzliste spezialisiert ist und die richtige Anzahl an Parametern besitzt, so wird die Methode zu der generischen Funktion in Form einer quotierten Lambdafunktion hinzugefügt. 

Die Methode \texttt{(define/public (attack) {\textquotesingle}attr)} aus dem Pokemon-Beispiel würde zu ihrer generischen Funktion beispielsweise als \texttt{($\lambda$ () {\textquotesingle}attr)} hinzugefügt. 

Wir können damit \texttt{enure-generic-functions} definieren:

\begin{lstlisting}
(define (ensure-generic-functions meta)
  ; keep track of new generic functions and replace the definition
  ; in meta with the meta objects
  (set-field! generic-functions
              meta
              (map (curryr make-generic-function meta)
                   (get-field generic-functions meta)))
  ; add  new methods to existing generic functions
  (for ([method (get-field direct-methods meta)])
    ; for each new method, if a generic function for it exists
    (when (is-generic? method meta)
      ; get generic function for it
      (let* ([gf (get-generic (get-name method))]
             [gf-params (send gf number-of-params)]
             [method-params (length (cdr (second method)))])
        ; if the generic function is defined for a superclass
        (when  (member (get-field meta-class gf)
                       (get-field class-precedence-list meta))
          ; and the method has the right amount of parameters
          (if (= gf-params method-params)
              ; add new method to generic function
              (send gf
                    add-method
                    meta
                    (method->lambda method))
              ; else, raise an error
              ...))))
  ; compute all apllicable generic functions for the class
  (set-field! applicable-generic-functions meta
              (applicable-generic-functions meta)))
\end{lstlisting}

Die Funktion benutzt drei Hilfsfunktionen:

\texttt{make-generic-function} erstellt ein Metaobjekt für die generische Funktion und fügt es zur Liste hinzu. Falls eine generische Funktion mit dem Namen bereits existiert, wird ein Fehler geworfen. Im Klassen-Metaobjekt wird anschließend die Definitionen der generischen Funktione durch das entprechende Metaobjekt ersetzt.

\texttt{method-$>\lambda$} wandelt die Methodendefinition in eine quotierte Lambdafunktion um.

\texttt{applicable-generic-functions} ermittelt alle anwendbaren generischen Funktionen. Dafür werden alle generischen Funktionen, die einer in der Klassenpräzedenzliste auftauchenden Klasse definiert werden, in einer Liste zusammengefügt.

Mit dem Aktualsieren der generischen Funktionen ist die Erzeugung des Klassen-Metaobjektes abgeschlossen.

\section{Erstellen des Klassenobjekts}

Wenn wir noch einmal die \texttt{expand!}-Funktion betrachten:

\begin{lstlisting}
(define (expand! my-eval supers args)
  (let* ([meta (ensure-class supers args)]
         [obj  (make-classobject my-eval supers args meta)])
    (add-class obj meta)
    obj))
\end{lstlisting}

so fehlt nun noch der zweite Teil: die Erzeugung des eigentlichen Klassenobjektes. Das wird von der Methode \texttt{make-class-object} übernommen. Sie erhält die Klassendefinition und das erzeugte Metaobjekt und erzeugt daraus einen eigenen \texttt{class}-Makro-Aufruf.

Zunächst müssen die \texttt{define/generic}-Optionen aus der Klassendefinition entfernt werden, denn Racket kennt keine solche Klassenoption. Danach gibt es zwei Fälle zu unterscheiden:
\begin{itemize}
 \item Falls die zu definierende Klasse nur eine einzelne Superklasse besitzt und keine generischen Funktionen beteiligt sind, so können wir die Klassenoptionen unverändert weiterverwenden und Racket die Vererbung überlassen. Das soll sicherstellen, dass bestehende Module, die das Objektsystem benutzen, von der Erweiterung nicht beeinflusst werden.
 \item Ansonsten erstellen wir eine neue Klasse, die die Eigenschaften der Superklassen vereint und nutzen sie als Superklasse für die neu definierte Klasse. Außerdem werden an  Methodenkombinationen beteiligte Methoden aus beiden Klassen entfernt und die kombinierten Methoden in die neue Klasse eingefügt.
\end{itemize}

Die zusammengesetzte Syntax des \texttt{class}-Aufrufes muss dann nur noch mit \texttt{my-eval} ausgewertet werden und wir erhalten die gewünschte Racket-Klasse:

\begin{lstlisting}
(define (make-classobject my-eval supers args meta)
  ; remove define/generic forms
  (let ([args (filter (negate generic-function-definition?) args)])
    ; if there's only a single superclass and no generic functions involved
    (if (racket-only? meta)
        ; then we can let racket handle object creation
        (my-eval (append '(class) supers args))
        ; else, put together the superclass and other class options
        ; and use them for the new class
        (my-eval (append '(class)
                         (generate-class-options my-eval meta)
                         ; other args, including the call to
                         ; super-new
                         (filter (negate (curryr is-generic? meta)) 
\end{lstlisting}

\texttt{racket-only?} erledigt die Überprüfung auf Anzahl der Superklassen und Beteiligung von generischen Funktionen. Beides lässt sich aus dem entsprechenden Feld der Metaklasse ermitteln. 

\begin{lstlisting}
(define (racket-only? meta)
  (and (= 1 (length (get-field direct-supers meta)))
       (empty? (get-field applicable-generic-functions meta))))
\end{lstlisting}

\texttt{generate-class-options} generiert die Klassenoptionen für das Erstellen einer Klasse mit Mehrfachvererbung oder Methodenkombination: zunächst die Superklasse; sie beinhaltet alle geerbten Felder sowie diejenigen geerbten Methoden, die nicht an Methodenkombination beteiligt sind. Falls Methodenkombination beteiligt ist, werden die kombinierten Methoden eingefügt.

\begin{lstlisting}
(define (generate-class-options my-eval meta)
  (let* ([inherited-fields  (get-field inherited-fields meta)]
         [inherited-methods (get-field inherited-methods meta)]
         [applicable-generic-functions 
          (get-field applicable-generic-functions meta)])
    (append
     ; superclass
     (list (my-eval (append '(class object% (super-new))
                            inherited-fields
                            (filter (negate (curryr is-generic? meta))
                                    inherited-methods))))
     ; add combination methods
     (map (curry combine-method meta)
          applicable-generic-functions)))))
\end{lstlisting}

\section{Methodenkombination}
\label{combination-impl}
Die Funktion \texttt{combine-methods} in \texttt{generate-class-options} führt die Methodenkombination durch. Sie erhält die Metaobjekte für die Klasse und die generische Funktion und gibt einen quotierten Ausdruck zurück, der, wenn ausgewertet, das Ergebnis der Methodenkombination berechnet.

Die Parameter, mit denen die Funktion später aufgerufen wird, werden auf alle implementierenden Methoden angewendet. Auf die Einzelergebnisse der Methoden wird anschließend die Kombinationsfunktion angewendet.

Nehmen wir zum Beispiel die generische Funktion \texttt{(foo x y)} mit \texttt{*} als Kombinationsfunktion und zwei implementierenden Methoden:

\texttt{($\lambda$ (x y) (+ x y))\\
($\lambda$ (x y) (- x y))}

Dann ist das Ergebnis von \texttt{combine-method} die Definition einer Racket-Klassenmethode, die die Kombination berechnet:

\begin{lstlisting}
'(define/public (foo x y)
   (apply + (map (curryr apply (list x y)
                               (list (lambda (x y) (+ x y))
                                     (lambda (x y) (- x y)))))))
\end{lstlisting}

Da die Syntax der kombinierten Methode zusammengesetzt wird ohne sie auszuwerten, muss mit quotierten Ausdrücken gearbeitet werden:

\begin{lstlisting}
(define (combine-method meta gf)
  (let* ([name (get-field name gf)]
         [params (get-field params gf)]
         [combination (get-field combination gf)]
         [functions (send gf applicable-methods meta)])
    (list 'define/public
          (cons name params)
          ; '+  '(3 4 5)  -> '(+ 3 4 5)
          (list 'apply
                combination
                ; '(1 2)  '(f g h) -> '((f 1 2) (g 1 2) (h 1 2))
                (list 'map
                      (list 'curryr
                            'apply
                            (cons 'list params))
                      (cons 'list functions))))))
\end{lstlisting}

Nach dem Hinzufügen der generierten Klassenoptionen wird das von \texttt{make-classobject} erzeugte Klassenobjekt schließlich von \texttt{expand!} zur Liste der erzeugten Klassen hinzugefügt und dann dem Benutzer als Rückgabewert geliefert. 

Die vollständie Implementatierung des Moduls befindet sich auf der beiliegenden CD.