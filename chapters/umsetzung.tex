\chapter{Umsetzung}  
\label{implementation}

\vspace{-0.4cm}
Im Rahmen dieser Arbeit ist ein Prototyp entstanden, der das Racketobjektsystem um Mehrfachvererbung und Methodenkombination erweitert. Ein redefiniertes \texttt{class}-Makro nimmt die übergebene Syntax, hält wichtige Informationen in Form von Metaobjekten fest und generiert schließlich die Syntax, die vom eigentlichen \texttt{class}-Makro ausgewertet wird.
\vspace{-0.2cm}

\section{eval im Definitionsfenster}
\vspace{-0.4cm}
Das erste Problem, das sich dabei stellt, ist, dass Racket es nicht erlaubt, mehrere Superklassen anzugeben. Es ist zwar möglich ein Makro zu schreiben, das mehrere Superklassen akzeptiert, aber der Makroaufruf, zu dem schlussendlich expandiert wird, darf nur eine Superklasse beinhalten. Das bedeutet, es muss aus den angegebenen Klassen per Hand eine Superklasse erzeugt werden, die alle Eigenschaften der Klassen in sich vereinigt. Diese Klasse wird dann als offizielle Superklasse angeben. Wie genau das funktioniert, wird später in diesem Kapitel erläutert. Die Grundidee ist, dass dafür die Klassenoptionen ebenjener Superklasse zusammengebaut, in einen \texttt{class}-Aufruf gesteckt und dann ausgewertet werden müssen.

\texttt{class} ist jedoch ein Makro, keine Funktion. Es lässt sich nicht einfach mit \texttt{apply} auf eine zusammengestellte Liste von Klassenoptionen anwenden. Die andere naheliegende Option ist \texttt{eval}, welche jedoch nicht ohne weiteres außerhalb der Interaktionskonsole benutzt werden kann. Das Bereitstellen von \texttt{eval} auch innerhalb des Definitionsfensters geschieht noch vor der eigentlichen Expansion des Makros, deshalb soll hier zuerst darauf eingegangen werden.

\texttt{eval} ist eine Funktion in Racket, die einen quotierten Ausdruck nimmt und ihn auswertet. Salopp gesagt entfernt die Funktion ein Quotierungszeichen (wenn es eins gibt):

\begin{lstlisting}
> (define x 42)
> 'x
\end{lstlisting}
{\rsymbol x}
\vspace{-1em}
\begin{lstlisting}
> (eval 'x)
\end{lstlisting}
{\routput 42}

Damit lässt sich insbesondere ein quotierter Funktionsaufruf auswerten:

\begin{lstlisting}
> (eval '(+ 1 2))
\end{lstlisting}
{\routput 3}

Wird im Definitionsfenster versucht, den gleichen Ausdruck auszuwerten, so wird ein Fehler signalisiert.

{\color{red}\ttfamily\small\hspace{5pt} +: unbound identifier;}\\
{\color{red}\ttfamily\small\hspace{5pt} also, no \#\%app syntax transformer is bound in: +}\\

\texttt{eval} kann die Bindungen des Kontextes, in dem es aufgerufen wird, nicht sehen. Das beinhaltet auch alle importierten Racketfunktionen, selbst \texttt{+}. Damit \texttt{eval} Zugriff auf die Umgebung hat, in der es aufgerufen wird, muss der Namensraum mitgeteilt werden. In der Interaktionskonsole passiert das automatisch; im Definitionsfenster muss es manuell geschehen. Dafür kann als zweiter Parameter ein Namensraumwert übergeben werden. 

Die folgenden Definitionen und Beispiele sind dem Racketguide für Namensräume \cite{racketguide-namespace} entnommen.

Ein Namensraum beinhaltet zwei Informationen:
\begin{enumerate}
 \item Eine Abbildung von Schlüsselwörtern zu Bindungen (zum Beispiel der Name \texttt{+} zu der Funktion \texttt{+}). Ein leerer Namensraum bildet jeden Namen auf eine uninitialisierte Variable ab.
 \item Eine Abbildung von Modulnamen zu Moduldeklarationen und Instanzen.
\end{enumerate}

Die erste Abbildung ist notwendig, um Ausdrücke wie das Additionsbeispiel auszuwerten. Die zweite Abbildung ermöglicht die Auswertung von \texttt{require}-Befehlen.

Ein leerer Namensraum kann mit der Funktion \texttt{make-empty-namespace} erzeugt werden. Da der Namensraum jedoch leer ist, verhält sich die \texttt{eval}-Funktion genauso als wäre kein Namensraum übergeben worden: die Auswertung schlägt fehl. Um einen Namensraum benutzbar zu machen, müssen Module aus dem aktuellen Namensraum angehängt werden. \texttt{make-base-empty-namespace} erzeugt beispielsweise einen Namensraum, an den das Modul \texttt{racket/base} bereits angehängt ist. Andere Module lassen sich mit \texttt{namespace-attach-module} anhängen:

\begin{lstlisting}
(namespace-attach-module (current-namespace)
                         'racket/class
                         (make-base-empty-namespace))
\end{lstlisting}

\texttt{current-namespace} liefert den aktuellen Namensraum. Er beinhaltet alle Bindungen, die innerhalb des Kontextes, in dem die Funktion aufgerufen wird, vorhanden sind. Das \texttt{racket/class}-Modul aus dem aktuellen Namensraum wird an den leeren Base-Namensraum angehängt.

Das Anhängen eines Moduls bewirkt, dass innerhalb des Namensraums nun eine Abbildung des Modulnamen existiert; Mappings für die Funktionen und Schlüsselwörter existieren noch nicht. Dem Namensraum wurde lediglich mitgeteilt, wo das Modul zu finden ist. Für das Importieren gibt es die Funktion \texttt{parameterize}:

\begin{lstlisting}
(parameterize ([current-namespace (make-base-empty-namespace)])
  (namespace-require 'racket/class))
\end{lstlisting}

Module, die nicht dem Namensraum angehängt sind, wie hier \texttt{racket/class}, werden neu geladen und instanziiert. Das bedeutet, dass für den Namensraum ein neuer, eigenständiger \texttt{class}-Datentyp erzeugt wird:

\begin{lstlisting}
> (require racket/class)
> (class? object%)
\end{lstlisting}
{\routput \#t}

\begin{lstlisting}
> (class?
   (parameterize ([current-namespace (make-base-empty-namespace)])
     (namespace-require 'racket/class) ; loads again
     (eval 'object%)))
\end{lstlisting}
{\routput \#f}

Falls die gleiche Instanz des Datentyps benutzt werden soll, so muss das Modul zuerst an den Namensraum angehängt werden:

\begin{lstlisting}
> (require racket/class)
> (class?
   (let ([ns (make-base-empty-namespace)])
     (namespace-attach-module (current-namespace)
                              'racket/class
                              ns)
     (parameterize ([current-namespace ns])
       (namespace-require 'racket/class) ; uses attached
       (eval 'object%))))
\end{lstlisting}
{\routput \#f}

Wird der Namensraum nur innerhalb eines Moduls benötigt, so kann ein Name\-space-An\-chor definiert werden:

\begin{lstlisting}
(define-namespace-anchor anchor)
(define ns (namespace-anchor->namespace anchor))
(define (my-eval x) (eval x ns))

> (my-eval '(+ 1 2))
\end{lstlisting}
{\routput 3}

Das hier entstehende Modul definiert jedoch ein Makro, das in einem anderen Modul expandiert wird; und somit auch in einem anderen Namensraum. Soll auf diesen Namensraum innerhalb unseres Moduls zugegriffen, so ist es notwendig die Funktion \texttt{my-eval} innerhalb der Makroexpansion zu definieren. Dafür wird die vorhergehende Vorgehensweise mit \texttt{namespace-attach-module} und \texttt{parameterize} benutzt. Um später in der eigentlichen Expansion des \texttt{class}-Makros drei Fälle unterscheiden zu können, wurde die ``Zwischenexpansion'' in ein eigenes Makro ausgelagert:

\begin{lstlisting}
(define-syntax my-class
  (syntax-rules ()
    ; Match (my-class ...)
    [(my-class arg . args)
     ; Create a new namespace.
     (let ([ns (make-base-namespace)])
       ; Attach modules of the current namespace.
       (namespace-attach-module (current-namespace) 'racket/class ns)
       (namespace-attach-module (current-namespace) 'racket/list ns)
       (namespace-attach-module (current-namespace) 'racket/function ns)
       ; Import identifiert bindings from attached modules.
       (parameterize ([current-namespace ns])
         (namespace-require 'racket/class)
         (namespace-require 'racket/list)
         (namespace-require 'racket/function))
       ; Create an eval function that uses the namespace
       ; and add it to the syntax of the class call.
       ; Then expand the original class macro.
       (my-eval-class (lambda (x) (eval x ns)) arg . args))]))
\end{lstlisting}

Das Makro matcht Aufrufe von \texttt{my-class}. Es erzeugt einen neuen Namensraum, hängt dann die im Kontext des Makroaufrufes gebundenen Module \texttt{racket/class}, \texttt{racket/list} und \texttt{racket/function} an und bindet sie anschließend ein. Der resultierende Namensraum wird für die Definition einer \texttt{eval}-Funktion benutzt, die zu der Syntax des \texttt{my-class}-Aufrufes hinzugefügt wird. Das anschließend aufgerufene Makro \texttt{my-eval-class} übernimmt die eigentliche Expansion.

Der Name des Makros ist \texttt{my-class}, damit intern weiterhin Racketklassen mittels \texttt{class} erzeugt werden können. Erst beim Export wird das Makro umbenannt:

\begin{lstlisting}
(provide (rename-out [my-class class]))
\end{lstlisting}

\section{Redefinition des class-Makros}
\vspace{-0.3cm}
In dem Makro \texttt{my-eval-class} werden drei Fälle unterschieden, je nachdem welche Superklassen angegeben wurden:\vspace{-0.3cm}
\begin{itemize}
 \item Eine einzelne Superklasse \vspace{-0.3cm}
 \item Die leere Liste \texttt{()} - wir interpretieren sie als \texttt{object\%}\vspace{-0.3cm}
 \item Eine Liste von Superklassen \texttt{($<$superclass1$>$ $<$superclass2$>$ ...)}
\end{itemize}

Bei allen drei Optionen wird die Funktion \texttt{expand!} mit der \texttt{my-eval}-Funktion, der Liste der Superklassen und der Liste der restlichen Klassenoptionen aufgerufen:

\begin{lstlisting}
(define-syntax my-eval-class
  (syntax-rules ()
    [(my-eval-class my-eval () . rest) 
     (expand! my-eval (list object%) 'rest)]
    [(my-eval-class my-eval (super ...) . rest)
     (expand! my-eval (list super ...) 'rest)]
    [(my-eval-class my-eval super . rest)
     (expand! my-eval (list super) 'rest)]))
\end{lstlisting}

In \texttt{expand!} wird analog zu CLOS ein Metaobjekt und ein Klassenobjekt anhand der angegebenen Klassendefinition erzeugt. Die Klasse wird zur Liste der erzeugten Klassen hinzugefügt und anschließend das Klassenobjekt zurückgegeben. Da Klassen in Racket  keine Namen haben, wird das Objekt selbst als Schlüssel in der Klassenliste benutzt.

\begin{lstlisting}
(define (expand! my-eval supers args)
  (let* ([meta (ensure-class supers args)]
         [obj  (make-classobject my-eval supers args meta)])
    (add-class obj meta)
    obj))
\end{lstlisting}

\texttt{ensure-class} hat die gleiche Funktionalität wie in der CLOS-Implementierung. Sie hält alle wichtigen Informationen über das gerade zu erzeugende Objekt fest. 

\begin{lstlisting}
(define (ensure-class supers args)
  (let* ([direct-supers     (map find-class supers)]  
         [direct-fields     (compute-direct-fields args)]
         [direct-methods    (filter method-definition? args)]
         [generic-functions (filter generic-function-definition? args)]
         [meta (make-object meta-class% direct-supers direct-fields
                 direct-methods generic-functions)])
    (finalize-inheritance meta)
    (ensure-generic-functions meta)
    meta))
\end{lstlisting}


Das passiert in drei Schritten: \vspace{-0.1cm}
\begin{enumerate}
 \item Es wird ein Metaobjekt mit den angegebenen Superklassen, Feldern, Methoden und generischen Funktionen erzeugt.\vspace{-0.1cm}
 \item \texttt{finalize-inheritance} fügt die Klassenpräzedenzliste, geerbte Felder und Methoden hinzu.\vspace{-0.1cm}
 \item \texttt{ensure-generic-functions} aktualisiert die Liste der generischen Funktionen. Neue generische Funktionen, die in der aktuellen Klasse definiert sind, werden hinzugefügt und Methoden, zu denen eine generische Funktion existiert, werden zu der generischen Funktion hinzugefügt.
\end{enumerate}

Das erzeugte Klassenmetaobjekt wird anschließend zurückgegeben, denn das tatsächliche Klassenobjekt muss ebenfalls erzeugt sein, bevor es zur Liste hinzugefügt werden kann. Die Erzeugung des Klassenobjekts erfolgt zum Schluss. Zunächst soll auf die Implementierung der Metaklassen für Klassen und generische Funktionen näher eingegangen werden.

\section{Klassenmetaobjekte}
Klassenmetaobjekte werden analog zu CLOS definiert. Es gibt eine interne Racketklasse für Metaobjekte, hier \texttt{meta-class\%} genannt, die sich alle wichtigen Informationen über Superklassen, Felder und Methoden in Form von Feldern merkt.

Im Gegensatz zu CLOS geschieht jedoch nur eine Manipulation der Syntax. Anstelle von Feld- und Methodenobjekten sind in der Metaklasse die Definitionen der Felder und Methoden abgelegt. Sie werden erst zum Schluss von Racket ausgewertet, als Teil der Definition des Klassenobjekts, das an den Nutzer zurückgegeben wird.

Für die Klasse Thing aus dem Object-Racketkapitel

\begin{lstlisting}
(define Thing (class object% (super-new)
                (init-field [name "a Thing"])
                (define/public (who-are-you?) 
                  (string-append "I am " name "!"))))
\end{lstlisting}

würde als direkte Felder die Liste 

\texttt{{\textquotesingle}((init-field [name {\qq}a Thing\qq]))} 

und als direkte Methoden die Liste 

\texttt{{\textquotesingle}((define/public (who-are-you?) (string-append {\qq}I am {\qq} name \qq!\qq)))} 

festgehalten. Eine Ausnahme bilden Superklassen und generische Funktionen. Klassen sind in Racket nicht benannt und der Benutzer hat die Freiheit die gleiche Klasse mehreren Variablen zuzuweisen. Um eine Klasse eindeutig identifizieren zu können, ist also die Referenz notwendig; dafür müssen die Superklassen ausgewertet werden. Nicht die eigentlichen Superklassen, sondern die zugehörigen Metaobjekte sind für die anschließenden Berechnungen relevant. Deshalb hält die Klassenpräzedenzliste die Präzedenz der Metaobjekte fest. 

Da generische Funktionen komplett selbst implementiert werden, bietet es sich für sie ebenfalls an, direkt das Generische-Funktion-Metaobjekt abzuspeichern. \texttt{ensure-class} legt in dem Feld zunächst die Definition der generischen Funktion ab; in der Funktion \texttt{ensure-generic-functions} werden die Definitionen dann durch die entsprechenden Metaobjekte ersetzt.

Um Klassen für Debugging unterscheiden zu können, erhält jede Klasse außerdem eine aufsteigende Nummer. Die Anzahl der bisher erzeugten Klassen wird in der Variable \texttt{num-of-classes} festgehalten.

Im Gegensatz zu CLOS werden am Ende der Expansion von einer Klasse mit mehreren Superklassen zwei Klassen erstellt: Die Klasse, welche die Eigenschaften der Superklassen zusammenfasst und die neue, durch das Makro definierte Klasse, die von der zusammengestellten Superklasse erbt. Das bedeutet, dass am Ende unterschieden wird zwischen geerbten Feldern und Methoden und solchen, die in der neuen Klasse definiert wurden. Es bietet sich daher an, anstelle der effektiven Felder und Methoden nur die geerbten festzuhalten. Die effektiven Eigenschaften lassen sich leicht in einer Methode als Vereinigung von direkten und geerbten Eigenschaften ermitteln; umgekehrt müsste jedesmal die Liste der effektiven Eigenschaften gefiltert werden, um nur die geerbten zu erhalten. 

Es ergibt sich demnach die folgende Definition für die Klassenmetaklasse:

\begin{lstlisting}
(define meta-class%
  (class object% (super-new)
    (init-field direct-supers
                direct-fields
                direct-methods
                generic-functions)
    (field [number (begin 
                     (set! num-of-classes (+ 1 num-of-classes))
                     num-of-classes)]
           [class-precedence-list '()]
           [inherited-fields '()]
           [inherited-methods '()]
           [applicable-generic-functions '()])    
    (define/public (effective-fields)
      (append direct-fields inherited-fields))
    (define/public (effective-methods)
      (append direct-methods inherited-methods))))
\end{lstlisting}

In einer Hashtabelle werden die bereits erzeugen Klassen als Paar (Klassenobjekt, Metaobjekt) festgehalten und es gibt die Methoden \texttt{findclass} und \texttt{add-class}, die den Zugriff erleichtern. Zu Beginn befindet sich bereits \texttt{object\%} und sein Metaobjekt in der Tabelle.

Die Superklassen, direkten Felder und Methoden lassen sich aus der Syntax der Klassendefinition extrahieren. Um Felder später unabhängig voneinander vererben zu können, werden \texttt{field}- und \texttt{init-field}-Optionen, die mehr als ein Feld definieren, gesplittet. 

\texttt{(field [a 1] [b 2])} 

wird beispielsweise durch

\texttt{(field [a 1]) (field [b 2])}

ersetzt. Die Methode \texttt{compute-direct-fields} übernimmt diese Konversion.

Die Eigenschaften, die sich durch Einfach- oder Mehrfachvererbung ergeben, werden durch die Methode \texttt{finalize-inheritance} zu dem Metaobjekt hinzugefügt. Sie berechnet die Klassenpräzedenzliste und anhand dieser die geerbten Felder und Methoden und bestückt die entsprechenden Felder des Metaobjekts.

Für die Berechnung der Klassenpräzedenzliste wird die Funktion \texttt{compute-std-cpl} aus der CLOS-Implementierung im Racketpaket \texttt{swindle} verwendet. Sie implementiert die Klassenpräzedenzliste bereits nach den in AMOP definierten Regeln. Die Funktion erhält das Objekt, für das die Klassenpräzedenzliste berechnet werden soll  sowie eine Funktion, die zu einem (Meta-)Objekt die direkten Superklassen zurückgibt -- und liefert die vollständige Klassenpräzedenzliste. Die Funktion befindet sich in \texttt{tinyclos.rkt}, wird jedoch nicht exportiert. Der Quelltext wurde in dieses Modul kopiert, damit Benutzer des Moduls nicht erst \texttt{tinyclos.rkt} um einen Exportbefehl ergänzen müssen. 

Die Methoden \texttt{compute-inherited-fields} und \texttt{compute-inherited-methods} nutzen dann die Klassenpräzedenzliste, um die geerbten Felder und Methoden zu berechnen. Sie führen damit insbesondere die \textit{standard method combination} (ohne Ergänzungsmethoden) durch.

Für jedes Objekt in der Klassenpräzedenzliste sammeln sie die dort definierten direkten Eigenschaften und fügen diese zu einer Liste zusammen. Anschließend werden Duplikate, also Felder und Methoden mit gleichem Namen, aus der Liste entfernt. Nur das erste Auftreten, das Feld oder die Methode der Klasse mit der höchsten Präzedenz, verbleibt in der Liste. Da das Vorgehen bei Methoden und Feldern sich nur geringfügig unterscheidet, wird die Logik in der Methode \texttt{compute-inherited-stuff} zusammengefasst. Der Parameter \texttt{direct-stuff} ist entweder \texttt{{\textquotesingle}direct-fields} oder \texttt{{\textquotesingle}direct-methods}:
\newpage
\begin{lstlisting}
(define (compute-inherited-stuff direct-stuff cpl)
  (let ([stuff '()])
    ; add the fields/methods of all superclasses to the list
    (for*/list ([metas cpl])
      (set! stuff (append stuff 
                          (dynamic-get-field direct-stuff metas))))
    ; only keep the first appearance of every field/method declaration
    (filter-first-occurence stuff)))
\end{lstlisting}

Die Funktion \texttt{filter-first-occurence} sorgt für die Entfernung von Duplikaten. Aus

\texttt{\textquotesingle((field [a 1]) (field [a 2]) (field [b 3]) (field [b 0]))}

wird beispielsweise

\texttt{\textquotesingle((field [a 1]) (field [b 3]))}.

Mit der bisher eingeführten Funktionalität werden bereits alle notwendigen Informationen für das Klassenmetaobjekt gesammelt.

\section{Generische-Funktionen-Metaobjekte}
Für generische Funktionen gibt es ebenfalls eine Struktur aus Metaobjekten. Die Metaklasse für generische Funktionen heißt \texttt{meta-generic-function\%} und merkt sich für eine generische Funktion den Namen, die Parameterliste, das Klassenmetaobjekt, in der sie definiert wurde, die Methodenkombination sowie die Liste der Definitionen von implementierenden Methoden. Bis auf die Liste der implementierenden Methoden können diese Informationen aus der Syntax der Klassendefinition abgeleitet werden.

Die implementierenden Methoden werden in der Form (Metaklasse, Methodendefinitionen) in einer Hashtabelle abgelegt. Die Verwaltung dieser Tabelle wird ebenfalls von der Generische-Funktionen-Metaklasse übernommen. Sie bietet Methoden zum Hinzufügen einer implementierenden Methode sowie zur Berechnung der anwendbaren Methoden für ein Klassenmetaobjekt.

Die anwendbaren Methoden (\emph{applicable methods}) werden anhand der Klassenpräzedenzliste des Klassenmetaobjekts bestimmt: die Liste der Methoden wird nach denjenigen Methoden gefiltert, die auf ein Objekt aus der Klassenpräzedenzliste spezialisiert sind. Falls keine anwendbaren Methoden existieren, so wird eine Fehlermethode zurückgegeben. 

Würde der Fehler direkt an dieser Stelle signalisiert, wäre der Benutzer gezwungen, für jede Klasse, die an der Methodenkombination beteiligt sein kann, auch eine implementierende Methode anzubieten. Eine Hierarchie wie im Pokemon-Beispiel, bei dem die Klasse Thing lediglich die generische Funktion \texttt{attack} definiert ohne eine implementierende Methode anzubieten, wäre nicht möglich. Dieses Verhalten soll jedoch erlaubt sein. Der Fehler soll erst auftreten, wenn der Benutzer tatsächlich versucht, die generische Funktion an einem Objekt der Klasse Thing aufzurufen. Das wird erreicht, indem die Methode, die den Fehler erzeugt, das Ergebnis der Methodenkombination ist.

Es ergibt sich damit folgende Definition für die Generische-Funktionen-Metaklasse:

\begin{lstlisting}
(define meta-generic-function%
  (class object% (super-new)
    (init-field name
                params
                meta-class
                combination) 
    (field [methods (make-hasheq)]) 
    (define/public (number-of-params) (length params))
    (define/public (add-method meta method) 
                     (hash-set! methods meta method))
    (define/public (applicable-methods meta) ... )))
\end{lstlisting}

Analog zu Klassen gibt es auch für generische Funktionen eine Tabelle mit dem Schema (Name, Generische-Funktion-Metaobjekt) und entsprechenden Zugriffsfunktionen \texttt{add-generic}, \texttt{get-generic} und \texttt{is-generic?}.

Die Verwaltung der Liste geschieht durch die Funktion \texttt{ensure-generic-functions}. 

Sie erhält das neu erzeugte Metaobjekt zu einer Klasse. Falls in der Klasse generische Funktionen definiert wurden, so werden sie zu der Liste hinzugefügt. Ist eine generische Funktion mit dem Namen bereits vorhanden, so wird ein Fehler signalisiert.

Anschließend werden die Methodendefinitionen verarbeitet. Wie bei Klas\-sen\-me\-ta\-ob\-jek\-ten werden auch Methoden und generische Funktionen über die Syntax ihrer Definition repräsentiert. Da Methoden darüber hinaus keine weiteren Informationen benötigen, gibt es für sie keine eigene Metaklasse. 

Falls eine generische Funktion für eine Methode existiert, die auf eine Klasse in der Präzedenzliste spezialisiert ist und die richtige Anzahl an Parametern besitzt, so wird die Methode zu der generischen Funktion in Form einer quotierten Lambdafunktion hinzugefügt. 

Die Methode \texttt{(define/public (attack) {\textquotesingle}attr)} aus dem Pokemon-Beispiel würde zu ihrer generischen Funktion beispielsweise als \texttt{($\lambda$ () {\textquotesingle}attr)} hinzugefügt. 

Es lässt sich damit die Funktion \texttt{enure-generic-functions} definieren:

\begin{lstlisting}
(define (ensure-generic-functions meta)
  ; keep track of new generic functions and replace the definition
  ; in meta with the meta objects
  (set-field! generic-functions
              meta
              (map (curryr make-generic-function meta)
                   (get-field generic-functions meta)))
  ; add  new methods to existing generic functions
  (for ([method (get-field direct-methods meta)])
    ; for each new method, if a generic function for it exists
    (when (is-generic? method meta)
      ; get generic function for it
      (let* ([gf (get-generic (get-name method))]
             [gf-params (send gf number-of-params)]
             [method-params (length (cdr (second method)))])
        ; if the generic function is defined for a superclass
        (when  (member (get-field meta-class gf)
                       (get-field class-precedence-list meta))
          ; and the method has the right amount of parameters
          (if (= gf-params method-params)
              ; add new method to generic function
              (send gf
                    add-method
                    meta
                    (method->lambda method))
              ; else, raise an error
              ...))))
  ; compute all apllicable generic functions for the class
  (set-field! applicable-generic-functions meta
              (compute-applicable-generic-functions meta)))
\end{lstlisting}

Die Funktion benutzt drei Hilfsfunktionen:
\begin{description}
\item[\texttt{make-generic-function}] erstellt ein Metaobjekt für die generische Funktion und fügt es zur Liste hinzu. Falls eine generische Funktion mit dem Namen bereits existiert, wird ein Fehler signalisiert. Im Klassenmetaobjekt wird anschließend die Definition der generischen Funktion durch das entprechende Metaobjekt ersetzt.

\item[\texttt{method-$>\lambda$}] wandelt die Methodendefinition in eine quotierte Lambdafunktion um.

\item[\texttt{compute-applicable-generic-functions}] ermittelt alle anwendbaren generischen Funktionen. Dafür werden die generischen Funktionen, die einer in der Klassenpräzedenzliste auftauchenden Klasse definiert werden, in einer Liste zusammengefügt.
\end{description}

Mit dem Aktualisieren der generischen Funktionen ist die Erzeugung des Klassenmetaobjekts abgeschlossen.

\section{Erstellen des Klassenobjekts}

Ein erneuter Blick auf die \texttt{expand!}-Funktion

\begin{lstlisting}
(define (expand! my-eval supers args)
  (let* ([meta (ensure-class supers args)]
         [obj  (make-classobject my-eval supers args meta)])
    (add-class obj meta)
    obj))
\end{lstlisting}

offenbart, dass noch der zweite Teil fehlt: die Erzeugung des eigentlichen Klassenobjekts. Das wird von der Methode \texttt{make-class-object} übernommen. Sie erhält die Klassendefinition und das erzeugte Metaobjekt und erzeugt daraus einen eigenen \texttt{class}-Makroaufruf.

Zunächst müssen die \texttt{define/generic}-Ausdrücke aus der Klassendefinition entfernt werden, denn Racket kennt keine solche Klassenoption. Danach gibt es zwei Fälle zu unterscheiden:
\begin{itemize}
 \item Falls die zu definierende Klasse nur eine einzelne Superklasse besitzt und keine generischen Funktionen beteiligt sind, so können die Klassenoptionen unverändert weiterverwendet und Racket die Vererbung überlassen werden. Das soll sicherstellen, dass bestehende Module, die das Objektsystem benutzen, von der Erweiterung nicht beeinflusst werden.
 \item Ansonsten wird eine neue Klasse erstellt, welche die Eigenschaften der Superklassen vereint und dannals Superklasse für die neu definierte Klasse genutzt. Außerdem werden an  Methodenkombinationen beteiligte Methoden aus beiden Klassen entfernt und die kombinierten Methoden in die neue Klasse eingefügt.
\end{itemize}

Auch bei Methodenkombination wird eine eigene Superklasse erstellt, selbst wenn in der Vererbungshierarchie keine Mehrfachvererbung auftritt. Der Grund hierfür ist, dass  für die Methodenkombination die implementierenden Methoden aus den Superklassen entfernt und die Kombinationsmethode in die neue Klasse eingefügt werden muss. Es wird demnach eine Superklasse benötigt, die Methoden, die in die Kombination eingehen, nicht beinhaltet.

Die zusammengesetzte Syntax des \texttt{class}-Aufrufes muss nur noch mit \texttt{my-eval} ausgewertet werden, um die gewünschte Racketklasse zu erhalten:

\begin{lstlisting}
(define (make-classobject my-eval supers args meta)
  ; remove define/generic forms
  (let ([args (filter (negate generic-function-definition?) args)])
  ; if there's only a single superclass and no generic functions involved
  (if (racket-only? meta)
      ; then we can let racket handle object creation
      (my-eval (append '(class) supers args))
      ; else, put together the superclass and other class options
      ; and use them for the new class
      (my-eval (append '(class)
                       ; superclass
                       (generate-superclass-expression my-eval meta)
                       ; other args, including the call to super-new
                       (filter (negate (curryr is-generic? meta)) args)
                       ; combination methods
                       (map (curry combine-method meta)
                            (get-field applicable-generic-functions
                                       meta)))))))
\end{lstlisting}

Die Hilfsfunktion \texttt{racket-only?} erledigt die Überprüfung auf Anzahl der Superklassen und Beteiligung von generischen Funktionen. Beides lässt sich aus dem entsprechenden Feld der Metaklasse ermitteln. 

\begin{lstlisting}
(define (racket-only? meta)
  (and (= 1 (length (get-field direct-supers meta)))
       (empty? (get-field applicable-generic-functions meta))))
\end{lstlisting}

\texttt{generate-superclass-expression} generiert die Superklasse. Sie beinhaltet alle geerbten Felder sowie diejenigen geerbten Methoden, die nicht an Methodenkombination beteiligt sind. 

\begin{lstlisting}
(define (generate-superclass-expression my-eval meta)
  (list (my-eval (append '(class object% (super-new))
                         (get-field inherited-fields meta)
                         ; only  methods that aren't
                         ; part of a method combination
                         (filter (negate (curryr is-generic? meta))
                                 (get-field inherited-methods meta))))))
\end{lstlisting}

Falls Methodenkombination beteiligt ist, werden am Ende der Klassendefinition noch die kombinierten Methoden eingefügt.

\section{Methodenkombination}
\label{combination-impl}
Die Funktion \texttt{combine-methods} in \texttt{generate-class-options} führt die Methodenkombination durch. Sie erhält die Metaobjekte für die Klasse und die generische Funktion und gibt einen quotierten Ausdruck zurück, der, wenn ausgewertet, das Ergebnis der Methodenkombination berechnet.

Die Parameter, mit denen die Funktion später aufgerufen wird, werden auf alle implementierenden Methoden angewendet. Auf die Einzelergebnisse der Methoden wird anschließend die Kombinationsfunktion angewendet.

Nehmen wir zum Beispiel die generische Funktion \texttt{(foo x y)} mit \texttt{*} als Kombinationsfunktion und zwei implementierenden Methoden:

\texttt{($\lambda$ (x y) (+ x y))\\
($\lambda$ (x y) (- x y))}

Dann ist das Ergebnis von \texttt{combine-method} die Definition einer Racketklassenmethode, welche die Kombination berechnet:

\begin{lstlisting}
'(define/public (foo x y)
   (apply + (map (curryr apply (list x y))
                 (list (lambda (x y) (+ x y))
                       (lambda (x y) (- x y))))))
\end{lstlisting}

Da die Syntax der kombinierten Methode zusammengesetzt wird ohne sie auszuwerten, muss mit quotierten Ausdrücken gearbeitet werden:

\begin{lstlisting}
(define (combine-method meta gf)
  (let* ([name (get-field name gf)]
         [params (get-field params gf)]
         [combination (get-field combination gf)]
         [functions (send gf applicable-methods meta)])
    (list 'define/public
          (cons name params)
          ; '+  '(3 4 5)  -> '(+ 3 4 5)
          (list 'apply
                combination
                ; '(1 2)  '(f g h) -> '((f 1 2) (g 1 2) (h 1 2))
                (list 'map
                      (list 'curryr
                            'apply
                            (cons 'list params))
                      (cons 'list functions))))))
\end{lstlisting}

Nach dem Hinzufügen der generierten Klassenoptionen wird das von der Funktion \texttt{make-classobject} erzeugte Klassenobjekt schließlich von \texttt{expand!} zur Liste der erzeugten Klassen hinzugefügt und dem Benutzer als Rückgabewert geliefert. 

Die vollständige Implementierung des Moduls befindet sich auf der beiliegenden CD.